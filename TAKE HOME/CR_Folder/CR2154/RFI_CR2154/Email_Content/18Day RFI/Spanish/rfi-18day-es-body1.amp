<!-- ||| COPY START -->
 %%[ 
 SET @Initial_VerificationCheckListDueDate_yearFull = FormatDate([Initial_VerificationCheckListDueDate],"YYYY")
 SET @Initial_VerificationCheckListDueDate_monthNumber = FormatDate([Initial_VerificationCheckListDueDate],"MM") 
 SET @Initial_VerificationCheckListDueDate_dayNumber = FormatDate([Initial_VerificationCheckListDueDate],"dd")
 ]%%
 <table align="center" border="0" cellpadding="0" cellspacing="0" class="w90" role="presentation" width="600">
    <tr>
        <td class="fontbody h2m" style="padding-top: 1px; padding-left: 20px; padding-right: 20px; text-align: left; font-size: 17px; line-height: 1.4em; color: #000000; font-family: Georgia, serif;">Recientemente le enviamos un aviso indicando los comprobantes que necesitamos para <b>el Caso N.&ordm;%%CaseNumber%%</b>. Env&iacute;e los comprobantes indicados antes de la las 4:30p.m., hora local fecha<b>&nbsp;%%=v(@Initial_VerificationCheckListDueDate_dayNumber)=%%/%%=v(@Initial_VerificationCheckListDueDate_monthNumber)=%%/%%=v(@Initial_VerificationCheckListDueDate_yearFull)=%%</b> para garantizar un procesamiento oportuno.<br>
%%[
Var @i, @j, @ProgramCodeName, @RFIspending, @counter 
SET @CaseNumber = [CaseNumber] 
SET @counter = 0 
SET @TypeOfAssistance = TypeOfAssistance 
SET @rows_CaseNumber = LookupOrderedRows("TEST_RFI_REMINDERS_PENDING_DE", 0,"ProgramCode asc", "CaseNumber", @CaseNumber) 
SET @rowCount_CaseNumber = rowcount(@rows_CaseNumber) 
IF @rowCount_CaseNumber > 0 THEN 
    FOR @i = 1 TO @rowCount_CaseNumber DO 
        SET @row_CaseNumber = row(@rows_CaseNumber, @i) /* get row based on counter */ 
        SET @ProgramCode = field(@row_CaseNumber,"ProgramCode") 
        IF @ProgramCode == 'KC' THEN 
            SET @ProgramCodeName = 'Kinship Care' 
        ELSEIF (@ProgramCode == 'QP' and @TypeOfAssistance == 'QHCP') THEN 
            SET @ProgramCodeName = 'QHCP'
        ELSEIF (@ProgramCode == 'QP') THEN 
            SET @ProgramCodeName = 'QUalified Health Plan' 
        ELSEIF @ProgramCode == 'CC' THEN 
            SET @ProgramCodeName = 'Child Care' 
        ELSEIF @ProgramCode == 'KT' THEN 
            SET @ProgramCodeName = 'KTAP' 
        ELSEIF (@ProgramCode == 'MA' and @TypeOfAssistance == 'BHP') THEN 
            SET @ProgramCodeName = 'Basic Health Plan (BHP)'
        ELSEIF (@ProgramCode == 'SN' and @TypeOfAssistance == 'ESAP') THEN 
            SET @ProgramCodeName = 'ESAP' 
        ELSEIF (@ProgramCode == 'MA' and @TypeOfAssistance == 'APTC') THEN 
            SET @ProgramCodeName = 'Qualified Health Plan with Payment Assistance (APTC)' 
        ELSEIF @ProgramCode == 'SN' THEN 
            SET @ProgramCodeName = 'SNAP' 
        ELSEIF @ProgramCode == 'FAD' THEN 
            SET @ProgramCodeName = 'FAD' 
        ELSE 
            SET @ProgramCodeName = "Medicaid/Payment Heath Assistance" 
        ENDIF /*SET @ValidSourceReceivedDate = field(@row_CaseNumber,"ValidSourceReceivedDate") */ 
        Var @ValidSourceReceivedDate 
        SET @Count_verified_docs = 0 
        SET @row_verified_docs = LookupRows("TEST_RFI_REMINDERS_PENDING_DE","CaseNumber", @CaseNumber, "ProgramCode", @ProgramCode) 
        SET @rowCount_verified_docs = rowcount(@row_verified_docs) 
        IF @rowCount_verified_docs > 0 THEN 
            FOR @x = 1 TO @rowCount_verified_docs DO 
                SET @rownum_verified_docs = row(@row_verified_docs, @x) /* get row based on counter */ 
                SET @ValidSourceReceivedDate = field(@rownum_verified_docs,"ValidSourceReceivedDate") 
                IF IsNull(@ValidSourceReceivedDate) THEN 
                    SET @Count_verified_docs = Add(@Count_verified_docs, 1) 
                ENDIF 
            NEXT @x 
        ENDIF 
        SET @VCLDescriptionES_i = field(@row_CaseNumber,"VCLDescriptionES") 
        IF @Count_verified_docs > 0 and @VCLDescriptionES_i != 'Verification of Auth Rep' THEN 
            IF @counter == 0 THEN 
]%%
        <br>Comprobantes necesarios para <b>%%=v(@ProgramCodeName)=%%:</b><br>
%%[
                SET @rows_ProgramCode = LookupOrderedRows("TEST_RFI_REMINDERS_PENDING_DE", 0,"VCLDescriptionES asc", "CaseNumber", @CaseNumber, "ProgramCode", @ProgramCode) 
                Var @VCLDescriptionES_prev 
                SET @rowCount_ProgramCode = rowcount(@rows_ProgramCode) 
                IF @rowCount_ProgramCode > 1 THEN 
                    SET @counter = Subtract(@rowCount_ProgramCode, 1) 
                ENDIF 
                IF @rowCount_ProgramCode > 0 THEN 
                    FOR @j = 1 TO @rowCount_ProgramCode DO 
                        SET @row_ProgramCode = row(@rows_ProgramCode, @j) 
                        SET @ValidSourceReceivedDate_inner = field(@row_ProgramCode,"ValidSourceReceivedDate") 
                        SET @VCLDescriptionES = field(@row_ProgramCode,"VCLDescriptionES") IF IsNull(@VCLDescriptionES_prev) or @VCLDescriptionES_prev != @VCLDescriptionES THEN 
                            SET @VCLDescriptionES_prev = field(@row_ProgramCode,"VCLDescriptionES") 
                            IF IsNull(@ValidSourceReceivedDate_inner) and @VCLDescriptionES != 'Verification of Auth Rep' THEN 
]%%
            <ul style="margin:1px 20px; padding: 0;">
                <li>%%=v(@VCLDescriptionES)=%%</li>
            </ul> 
%%[ 
                            ENDIF 
                        ENDIF 
                    NEXT @j 
                ENDIF 
            ELSE 
                SET @counter = Subtract(@counter, 1) 
            ENDIF 
        ENDIF 
    NEXT @i 
ENDIF 
]%%
            <br>Una vez que haya reunido los comprobantes, env&iacute;elos usando el bot&oacute;n a continuaci&oacute;n. Puede encontrar ejemplos de comprobantes aceptables en el aviso de su<b> <a alias="txt_kynectmessagecenter" conversion="false" data-linkto="http://" href="https://kynect.ky.gov/benefits/s/message-center" style="color: #704D9F; text-decoration:none;" title="centro de mensajes de kynect">centro de mensajes de kynect</a></b>.<p></p>
        </td>
    </tr>
</table>